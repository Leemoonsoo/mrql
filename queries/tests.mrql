function fact ( n: int ): long {
   if n <= 0
   then 1
   else n*fact(n-1)
};

fact(10);

[1,2]+[1,2,3];

{("a",1),("b",2)}["a"];

store xx := Node("a",{("x","1")},[Node("b",{},[CData("text")])]);

xx.b;

toInt(xx.@x)+1;

1 as long;

1 as double;

store x := [xml| <a A="2"><b>1</b><b>2</b></a> |];

x.b;

function children ( tag: string, s: list(XML) ) : list(XML) {
  select c from Node(t,*,cs) in s, c in cs where t=tag order by c
};

children("b",x.b);

min({1,2,3});

max({1.2,3.4});

min({1.3 as double,4.8 as double});

avg({1,2,3});

avg({1.2,3.4});

avg({1.3 as double,4.8 as double});

E = source(line,"queries/employee.txt",",",type(<name:string,dno:int,address:string>));
D = source(line,"queries/department.txt",",",type(<name:string,dno:int>));

select e.name from e in E where e.dno=10;

sum(select e.dno from e in E);

select n from <dno:10,name:n> in E;

select e from e in E;

select e.dno+11 from e in E;

select (e.name,d.name) from e in E, d in D where e.dno=d.dno;

select (e.name,d.name) from e in E, d in D where e.dno=d.dno and e.name="John Smith";

select (e.name,d.name) from e in E, d in D where e.dno=d.dno and e.name="John Smith" and d.name="CSE";

select d.name from d in D where all e in (select e from e in E where e.dno=d.dno): e.address="a";

select d.name from d in D where all(select e.address="a" from e in E where e.dno=d.dno);

select e from e in E order by inv(e.dno);

select e from e in E order by e.address;

select (e.name,d.name) from e in E, d in D where e.dno=d.dno order by e.address;

select (k,e) from e in E group by k: e.dno;

(select (k,e) from e in E group by k: e.dno)[10];

(select e from e in E order by e.dno)[1:2];

select (k,e) from e in E group by k: e.dno order by inv(k);

select (k,count(e)) from e in E group by k: e.dno order by count(e);

all e in E: e.dno > 1;

aggregation aggr(\(x:int,y:int):int.x+y,0):int;

aggr(select e.dno from e in E);

select (k,aggr(e.dno)) from e in E group by k: e.dno;

function ff (x:int,y:int):int { x+y };

aggregation aggr2(ff,0):int;

aggr2(select e.dno from e in E);

select (k,aggr2(e.dno)) from e in E group by k: e.dno;

select (e,x) from e in E, x in 1..10 where e.dno = x;

function g ( s: !bag(<name:string,dno:int,address:string>) ): !bag(<A:string,B:int>) {
   select <A:x.name,B:x.dno> from x in s
};

select (e,d)
from e in g(select x from x in E where x.name="A"),
     d in D
where e.B=d.dno;

/*****************************************************************************/

C = source(line,"/home/fegaras/tpch/data2/customer.tbl","|",type(<CUSTKEY:int,NAME:string,ADDRESS:string>));
O = source(line,"/home/fegaras/tpch/data2/orders.tbl","|",type(<ORDERKEY:int,CUSTKEY:int,ORDERSTATUS:string,TOTALPRICE:float>));

select (key,sum(p)) from <CUSTKEY:k,TOTALPRICE:p> in O group by key: k;

store A := {<A:1,B:2.0>,<A:2,B:3.0>,<A:1,B:2.0>};
select (key,sum(p)) from <A:k,B:p> in A group by key: k;

C;

C union C;

C intersect C;

select x from x in C where x.CUSTKEY = x.CUSTKEY-1;

select (c.NAME, o.TOTALPRICE) from c in C, o in O where o.CUSTKEY=c.CUSTKEY;

avg(select o.TOTALPRICE from c in C, o in O where o.CUSTKEY=c.CUSTKEY);

select (x,y) from x in C, y in C where x.CUSTKEY=y.CUSTKEY;

select (n, avg(select x.TOTALPRICE from x in o)) from c in C, o in O where o.CUSTKEY=c.CUSTKEY group by (k,n): (c.CUSTKEY,c.NAME);

select <N: c.NAME, A: avg(select o.TOTALPRICE from o in O where o.CUSTKEY=c.CUSTKEY)> from c in C;

select <N: c.NAME, A: avg(select o.TOTALPRICE from o in O where o.CUSTKEY=c.CUSTKEY)> from c in C order by c.ADDRESS;

select avg(select x.TOTALPRICE from x in o) from o in O group by k: o.CUSTKEY;

select (k,avg(select x.TOTALPRICE from x in o)) from o in O group by k: o.CUSTKEY;

select (key,avg(p)) from <CUSTKEY:k,TOTALPRICE:p> in O group by key: k;

select (key,avg(p)) from <CUSTKEY:k,TOTALPRICE:p> in O group by key: k having avg(p)>100.0;

select (key,avg(p)) from <CUSTKEY:k,TOTALPRICE:p> in O group by key: k order by inv(key);

select (key,avg(p)) from <CUSTKEY:k,TOTALPRICE:p> in O group by key: k order by inv(count(p));

count(select (key,avg(p)) from <CUSTKEY:k,TOTALPRICE:p> in O group by key: k);


/*****************************************************************************/

source(xml,"/home/fegaras/data/dblp2.xml",{"inproceedings"},xpath(.[author="Leonidas Fegaras"]/title));

source(xml,"/home/fegaras/data/dblp2.xml",{"inproceedings"},xpath(.[year=2005]/title));

source(xml,"queries/cs.xml",{"gradstudent"},xpath(name[firstname="Leonidas"]));

select s.name
from s in source(xml,"queries/cs.xml",{"gradstudent"})
where text(s.name.firstname)="Leonidas";

select text(a.title)
from a in source(xml,"/home/fegaras/data/dblp2.xml",{"inproceedings"})
where some n in a.author: text(n)="Leonidas Fegaras";

count(source(xml,"/home/fegaras/data/dblp2.xml",{"inproceedings"},xpath(cite)));

count(source(xml,"/home/fegaras/data/dblp2.xml",{"book"},xpath(isbn)));

count(source(xml,"/home/fegaras/data/dblp2.xml",{"title"}));

select (a.@key,select text(c) from c in a.cite)
from a in source(xml,"/home/fegaras/data/dblp2.xml",{"inproceedings"});

select (x,count(a),count(a))
from a in source(xml,"/home/fegaras/data/dblp2.xml",{"inproceedings"}),
     c in a.cite
group by x: text(c);

select (x,count(a))
from a in source(xml,"/home/fegaras/data/dblp2.xml",{"inproceedings"}),
     c in a.cite
where text(c) <> "..."
group by x: text(c)
order by inv(count(a));

select (select a.title from a in source(xml,"/home/fegaras/data/dblp2.xml",{"inproceedings"})
         where a.@key = text(c))
from a in source(xml,"/home/fegaras/data/dblp2.xml",{"book"}),
     c in a.cite
where text(c) <> "...";

select text(c)
from a in source(xml,"/home/fegaras/data/dblp2.xml",{"book"}),
     c in a.cite
where text(c) <> "...";
